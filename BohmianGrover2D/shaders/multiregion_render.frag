#version 300 es
precision highp float;
precision highp sampler2D;

uniform sampler2D uWave;
uniform float uVisGain;
uniform float uVisGamma;
uniform int uShowPhase;
uniform int uShowGrid;
uniform int uTarget;
uniform int uGateRegion;
uniform float uGateFlash;
uniform float uPixelSize;
in vec2 vUV;
out vec4 fragColor;

vec3 hsv2rgb(vec3 c) {
  vec3 p = abs(fract(c.xxx + vec3(0.0, 2.0/3.0, 1.0/3.0)) * 6.0 - 3.0);
  return c.z * mix(vec3(1.0), clamp(p - 1.0, 0.0, 1.0), c.y);
}
vec3 phasePalette(float phase, float intensity) {
  float hue = fract(phase / 6.28318530718 + 1.0);
  float structure = smoothstep(.015, .20, intensity);
  return hsv2rgb(vec3(hue, mix(.28, .94, structure), .075 + .925 * intensity));
}
vec3 densityPalette(float t) {
  return vec3(.22,.32,.28) + vec3(.40,.45,.35) * cos(6.283185 * (t + vec3(.15,.55,.75)));
}
vec2 regionCenter(int q) { return (vec2(float(q / 4), float(q % 4)) + .5) / 4.0; }
float regionMask(vec2 uv, int q) {
  vec2 distance = abs(uv - regionCenter(q));
  return (1.0 - smoothstep(.123, .127, distance.x)) * (1.0 - smoothstep(.123, .127, distance.y));
}

void main() {
  vec2 uv = clamp(vUV, 0.0, 1.0);
  vec2 texel = 1.0 / vec2(textureSize(uWave, 0));
  vec2 psi = texture(uWave, mix(.5 * texel, 1.0 - .5 * texel, uv)).rg;
  float rho = dot(psi, psi);
  float intensity = pow(clamp(1.0 - exp(-uVisGain * rho), 0.0, 1.0), uVisGamma);
  float phase = rho > 1e-20 ? atan(psi.y, psi.x) : 0.0;
  vec3 col = uShowPhase == 1 ? phasePalette(phase, intensity) : densityPalette(intensity) * intensity;

  float wall = min(min(uv.x, uv.y), min(1.0 - uv.x, 1.0 - uv.y));
  float rim = 1.0 - smoothstep(uPixelSize, 3.0 * uPixelSize, wall);
  col = mix(col, vec3(.35,.94,1.0), .82 * rim);
  col += vec3(.05,.38,.88) * exp(-wall * 96.0) * .30;
  if (uShowGrid == 1) {
    vec2 nearest = abs(uv * 4.0 - floor(uv * 4.0 + .5)) / 4.0;
    float lines = 1.0 - smoothstep(.6 * uPixelSize, 1.7 * uPixelSize, min(nearest.x, nearest.y));
    col = mix(col, vec3(.18,.55,.72), .45 * lines);
  }
  vec2 d = abs(uv - regionCenter(uTarget));
  float edge = min(abs(d.x - .123), abs(d.y - .123));
  float within = step(d.x, .125) * step(d.y, .125);
  float glow = within * (1.0 - smoothstep(.004, .025, edge));
  float border = within * (1.0 - smoothstep(.7 * uPixelSize, 2.2 * uPixelSize, edge));
  col = mix(col, col * vec3(1.06,.76,.80) + vec3(.13,.005,.012), .20 * regionMask(uv, uTarget));
  col += vec3(1.0,.015,.035) * (.34 * glow + 1.05 * border);
  if (uGateRegion >= 0) col += vec3(1.0,.28,.72) * regionMask(uv, uGateRegion) * uGateFlash * .16;
  fragColor = vec4(col, 1.0);
}
